<?xml version="1.0" encoding="utf-8"?>
<mx:HDividedBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%"
                xmlns:controls="ns.flex.controls.*"
                addedToStage="stage.addEventListener(MouseEvent.CLICK, onStageClick)"
                creationComplete="cc()">
    <mx:Metadata>
        [Event(name="windowClose")]
        [Event(name="timerComplete")]
    </mx:Metadata>
    <mx:Script>
		<![CDATA[
        import mx.containers.ViewStack;
        import mx.core.Container;
        import mx.managers.HistoryManager;

        import ns.flex.support.MenuSupport;

        [Bindable]
        public var menuTitle:String = '菜单';

        [Bindable]
        public var menus:XML;
        public var spareMintues:int;
        public var views:ViewStack;
        [Bindable]
        public var appTitle:String = '';
        private var _historyManager:HistoryManager;
        private var timer:Timer;

        public function initTimer(mintues:int):void
        {
            removeTimer()
            if (mintues > 0)
            {
                spareMintues = mintues;
                timer = new Timer(60 * 1000, mintues);
                timer.start();
                timer.addEventListener(TimerEvent.TIMER_COMPLETE, timerComplete);
            }
        }

        public function removeTimer():void
        {
            if (timer)
            {
                timer.removeEventListener(TimerEvent.TIMER_COMPLETE, timerComplete);
                timer.stop();
            }
        }

        public function resetTimer():void
        {
            if (timer)
            {
                timer.reset();
                timer.start();
            }
        }

        private function cc():void
        {
            initTimer(spareMintues)
            appWindow.addChild(views);

            var ms:MenuSupport = new MenuSupport(menuPanel);
            ms.createMenuItem('展开', function (e:Event):void
            {
                menuTree.expandChildrenOf(menus.node[0], true)
            }, true, true);
            ms.createMenuItem('收起', function (e:Event):void
            {
                menuTree.expandChildrenOf(menus.node[0], false)
            }, false, true);
        }

        //用上下键切换菜单时，可能由于SWFLoader未及时完成的原因，报空指针错误
        //Flex官方sample：Explorer，也存在同样的问题。
        //所以把键盘事件屏蔽,并且每次初始化一个SWFLoader实例
        private function menuClick():void
        {
            if (!menuTree.selectedItem)
                return;
            var nodeApp:String = menuTree.selectedItem.@app;

            if (nodeApp)
            {
                var app:DisplayObject = views.getChildByName(nodeApp);
                views.selectedChild = (app ? app : views.getChildByName('ComingModule')) as Container;
                appWindow.title = String(menuTree.selectedItem.@label).concat(appTitle);
            }
            else
                menuTree.expandItem(menuTree.selectedItem,
                        !menuTree.isItemOpen(menuTree.selectedItem), true);

        }

        private function onStageClick(e:MouseEvent):void
        {
            trace('x: ', e.stageX, ' y: ', e.stageY);
            resetTimer();
        }

        private function switchMenu():void
        {
            if (hdb.numChildren > 1)
                hdb.removeChild(menuPanel);
            else
                hdb.addChildAt(menuPanel, 0);
        }

        private function timerComplete(e:Event):void
        {
            dispatchEvent(new Event('timerComplete'));
        }
        ]]>
	</mx:Script>
    <mx:HDividedBox id="hdb" width="100%" height="100%">
        <mx:Panel id="menuPanel" width="200" height="100%" title="{menuTitle}">
            <mx:Tree id="menuTree" width="100%" height="100%" wordWrap="false"
                     showRoot="false" dataProvider="{menus.node}" labelField="@label"
                     click="menuClick()"/>
        </mx:Panel>
        <controls:TitleWindowPlus id="appWindow" width="100%" height="100%"
                                  showCloseButton="true" title="{appTitle}"
                                  close="dispatchEvent(new Event('windowClose'))"
                                  titleDoubleClick="switchMenu()">
        </controls:TitleWindowPlus>
    </mx:HDividedBox>
</mx:HDividedBox>
